       1/       0 :                    ;       NAM    MINIB
       2/       0 :                    * MINI-BUG
       3/       0 :                    * COPYWRITE 1973, MOTOROLA INC
       4/       0 :                    * REV 004 (USED WITH MIKBUG)
       5/       0 : =FCF4              ACIACS EQU    @176364  ; ACIA CONTROL/STATUS
       6/       0 : =FCF5              ACIADA EQU    ACIACS+1
       7/    FE00 :                           ORG    $FE00
       8/    FE00 :                    * MINIB
       9/    FE00 :                    * INPUT ONE CHAR INTO A-REGISTER
      10/    FE00 : B6 FC F4           INCH   LDAA   ACIACS
      11/    FE03 : 47                        ASRA
      12/    FE04 : 24 FA                     BCC    INCH     ; RECEIVE NOT READY
      13/    FE06 : B6 FC F5                  LDAA   ACIADA   ; INPUT CHARACTER
      14/    FE09 : 84 7F                     ANDA   #$7F     ; RESET PARITY BIT
      15/    FE0B : 81 7F                     CMPA   #$7F
      16/    FE0D : 27 F1                     BEQ    INCH     ; RUBOUT; IGNORE
      17/    FE0F : 7E FE AE                  JMP    OUTCH    ; ECHO CHAR
      18/    FE12 :
      19/    FE12 :                    * INPUT HEX CHAR
      20/    FE12 : 8D EC              INHEX  BSR    INCH
      21/    FE14 : 81 30                     CMPA   #$30
      22/    FE16 : 2B 52                     BMI    C1       ; NOT HEX
      23/    FE18 : 81 39                     CMPA   #$39
      24/    FE1A : 2F 0A                     BLE    IN1HG
      25/    FE1C : 81 41                     CMPA   #$41
      26/    FE1E : 2B 4A                     BMI    C1       ; NOT HEX
      27/    FE20 : 81 46                     CMPA   #$46
      28/    FE22 : 2E 46                     BGT    C1       ; NOT HEX
      29/    FE24 : 80 07                     SUBA   #7
      30/    FE26 : 39                 IN1HG  RTS
      31/    FE27 :
      32/    FE27 : 86 D1              LOAD   LDAA   #$D1     ; TURN READER ON
      33/    FE29 : B7 FC F4                  STAA   ACIACS
      34/    FE2C : 86 11                     LDAA   #@21
      35/    FE2E : 8D 7E                     BSR    OUTCH
      36/    FE30 :
      37/    FE30 : 8D CE              LOAD3  BSR    INCH
      38/    FE32 : 81 53                     CMPA   #'S
      39/    FE34 : 26 FA                     BNE    LOAD3    ; 1ST CHAR NOT (S)
      40/    FE36 : 8D C8                     BSR    INCH
      41/    FE38 : 81 39                     CMPA   #'9
      42/    FE3A : 27 25                     BEQ    LOAD21
      43/    FE3C : 81 31                     CMPA   #'1
      44/    FE3E : 26 F0                     BNE    LOAD3    ; 2ND CHAR NOT (1)
      45/    FE40 : 7F FF 32                  CLR    CKSM     ; ZERO CHECKSUM
      46/    FE43 : 8D 36                     BSR    BYTE     ; READ BYTE
      47/    FE45 : 80 02                     SUBA   #2
      48/    FE47 : B7 FF 33                  STAA   BYTECT   ; BYTE COUNT
      49/    FE4A :                    * BUILD ADDRESS
      50/    FE4A : 8D 21                     BSR    BADDR
      51/    FE4C :                    * STORE DATA
      52/    FE4C : 8D 2D              LOAD11 BSR    BYTE
      53/    FE4E : 7A FF 33                  DEC    BYTECT
      54/    FE51 : 27 05                     BEQ    LOAD15   ; ZERO BYTE COUNT
      55/    FE53 : A7 00                     STAA   0,X      ; STORE DATA
      56/    FE55 : 08                        INX
      57/    FE56 : 20 F4                     BRA    LOAD11
      58/    FE58 :
      59/    FE58 : 7C FF 32           LOAD15 INC    CKSM
      60/    FE5B : 27 D3                     BEQ    LOAD3
      61/    FE5D : 86 3F              LOAD19 LDAA   #'?      ; PRINT QUESTION MARK
      62/    FE5F : 8D 4D                     BSR    OUTCH
      63/    FE61 : 86 B1              LOAD21 LDAA   #$B1     ; TURN READER OFF
      64/    FE63 : B7 FC F4                  STAA   ACIACS
      65/    FE66 : 86 13                     LDAA   #@23
      66/    FE68 : 8D 44                     BSR    OUTCH
      67/    FE6A : 7E FE DB           C1     JMP    CONTRL
      68/    FE6D :
      69/    FE6D :                    * BUILD ADDRESS
      70/    FE6D : 8D 0C              BADDR  BSR    BYTE     ; READ 2 FRAMES
      71/    FE6F : B7 FF 34                  STAA   XHI
      72/    FE72 : 8D 07                     BSR    BYTE
      73/    FE74 : B7 FF 35                  STAA   XLOW
      74/    FE77 : FE FF 34                  LDX    XHI      ; (X) ADDRESS WE BUILT
      75/    FE7A : 39                        RTS
      76/    FE7B :
      77/    FE7B :                    * INPUT BYTE (TWO FRAMES)
      78/    FE7B : 8D 95              BYTE   BSR    INHEX    ; GET HEX CHAR
      79/    FE7D : 48                        ASLA
      80/    FE7E : 48                        ASLA
      81/    FE7F : 48                        ASLA
      82/    FE80 : 48                        ASLA
      83/    FE81 : 16                        TAB
      84/    FE82 : 8D 8E                     BSR    INHEX
      85/    FE84 : 84 0F                     ANDA   #$0F     ; MASK TO 4 BITS
      86/    FE86 : 1B                        ABA
      87/    FE87 : 16                        TAB
      88/    FE88 : FB FF 32                  ADDB   CKSM
      89/    FE8B : F7 FF 32                  STAB   CKSM
      90/    FE8E : 39                        RTS
      91/    FE8F :
      92/    FE8F :                    * CHANGE MEMORY (M AAAA DD NN)
      93/    FE8F : 8D DC              CHANGE BSR    BADDR    ; BUILD ADDRESS
      94/    FE91 : 8D 34                     BSR    OUTS     ; PRINT SPACE
      95/    FE93 : 8D 30                     BSR    OUT2HS
      96/    FE95 : 8D E4                     BSR    BYTE
      97/    FE97 : 09                        DEX
      98/    FE98 : A7 00                     STAA   0,X
      99/    FE9A : A1 00                     CMPA   0,X
     100/    FE9C : 26 BF                     BNE    LOAD19   ; MEMORY DID NOT CHANGE
     101/    FE9E : 20 3B                     BRA    CONTRL
     102/    FEA0 :
     103/    FEA0 : 44                 OUTHL  LSRA            ; OUT HEX LEFT BCD DIGIT
     104/    FEA1 : 44                        LSRA
     105/    FEA2 : 44                        LSRA
     106/    FEA3 : 44                        LSRA
     107/    FEA4 :
     108/    FEA4 : 84 0F              OUTHR  ANDA   #$F      ; OUT HEX RIGHT BCD DIGIT
     109/    FEA6 : 8B 30                     ADDA   #$30
     110/    FEA8 : 81 39                     CMPA   #$39
     111/    FEAA : 23 02                     BLS    OUTCH
     112/    FEAC : 8B 07                     ADDA   #$7
     113/    FEAE :
     114/    FEAE :                    * OUTPUT ONE CHAR
     115/    FEAE : 37                 OUTCH  PSHB            ; SAVE B-REG
     116/    FEAF : F6 FC F4           OUTC1  LDAB   ACIACS
     117/    FEB2 : 57                        ASRB
     118/    FEB3 : 57                        ASRB
     119/    FEB4 : 24 F9                     BCC    OUTC1    ; XMIT NOT READY
     120/    FEB6 : B7 FC F5                  STAA   ACIADA   ; OUTPUT CHARACTER
     121/    FEB9 : 33                        PULB
     122/    FEBA : 39                        RTS
     123/    FEBB :
     124/    FEBB : A6 00              OUT2H  LDAA   0,X      ; OUTPUT 2 HEX CHAR
     125/    FEBD : 8D E1                     BSR    OUTHL    ; OUT LEFT HEX CHAR
     126/    FEBF : A6 00                     LDAA   0,X
     127/    FEC1 : 8D E1                     BSR    OUTHR    ; OUT RIGHT HEX VHAR
     128/    FEC3 : 08                        INX
     129/    FEC4 : 39                        RTS
     130/    FEC5 :
     131/    FEC5 : 8D F4              OUT2HS BSR    OUT2H    ; OUTPUT 2 HEX CHAR + SPACE
     132/    FEC7 : 86 20              OUTS   LDAA   #$20     ; SPACE
     133/    FEC9 : 20 E3                     BRA    OUTCH    ; (BSR & RTS)
     134/    FECB :
     135/    FECB :
     136/    FECB :                    * PRINT CONTENTS OF STACK
     137/    FECB : 30                 PRINT  TSX
     138/    FECC : FF FF 30                  STX    SP       ; SAVE STACK POINTER
     139/    FECF : C6 09                     LDAB   #9
     140/    FED1 : 8D F2              PRINT2 BSR    OUT2HS   ; OUT 2 HEX & SPCACE
     141/    FED3 : 5A                        DECB
     142/    FED4 : 26 FB                     BNE    PRINT2
     143/    FED6 :
     144/    FED6 :                    * ENTER POWER ON SEQUENCE
     145/    FED6 : =FED6              START  EQU    *
     146/    FED6 :                    * INZ ACIA
     147/    FED6 : 86 B1                     LDAA   #$B1     ; SET SYSTEM PARAMETERS
     148/    FED8 : B7 FC F4                  STAA   ACIACS
     149/    FEDB :
     150/    FEDB : 8E FF 28           CONTRL LDS    #STACK   ; SET STACK POINTER
     151/    FEDE : 86 0D                     LDAA   #$D      ; CARRIAGE RETURN
     152/    FEE0 : 8D CC                     BSR    OUTCH
     153/    FEE2 : 86 0A                     LDAA   #$A      ; LINE FEED
     154/    FEE4 : 8D C8                     BSR    OUTCH
     155/    FEE6 :
     156/    FEE6 : BD FE 00                  JSR    INCH     ; READ CHARACTER
     157/    FEE9 : 16                        TAB
     158/    FEEA : 8D DB                     BSR    OUTS     ; PRINT SPACE
     159/    FEEC : C1 4C                     CMPB   #'L
     160/    FEEE : 26 03                     BNE    *+5
     161/    FEF0 : 7E FE 27                  JMP    LOAD
     162/    FEF3 : C1 4D                     CMPB   #'M
     163/    FEF5 : 27 98                     BEQ    CHANGE
     164/    FEF7 : C1 50                     CMPB   #'P
     165/    FEF9 : 27 D0                     BEQ    PRINT    ; STACK
     166/    FEFB : C1 47                     CMPB   #'G
     167/    FEFD : 26 DC                     BNE    CONTRL
     168/    FEFF : 3B                        RTI             ; GO
     169/    FF00 :
     170/    FF00 :
     171/    FF00 :                           ORG    $FF00
     172/    FF00 :                           RMB    40
     173/    FF28 :                    STACK  RMB    1        ; STACK POINTER
     174/    FF29 :                    * REGISTERS FOR GO
     175/    FF29 :                           RMB    1        ; CONDITION CODES
     176/    FF2A :                           RMB    1        ; B ACCUMULATOR
     177/    FF2B :                           RMB    1        ; A
     178/    FF2C :                           RMB    1        ; X-HIGH
     179/    FF2D :                           RMB    1        ; X-LOW
     180/    FF2E :                           RMB    1        ; P-HIGH
     181/    FF2F :                           RMB    1        ; P-LOW
     182/    FF30 :                    SP     RMB    1        ; S-HIGH
     183/    FF31 :                           RMB    1        ; S-LOW
     184/    FF32 :                    * END REGISTERS FOR GO
     185/    FF32 :                    CKSM   RMB    1        ; CHECKSUM
     186/    FF33 :                    BYTECT RMB    1        ; BYTE COUNT
     187/    FF34 :                    XHI    RMB    1        ; XREG HIGH
     188/    FF35 :                    XLOW   RMB    1        ; XREG LOW
     189/    FF36 :                           END
